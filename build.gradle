plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'bmstu-schedule-loader'
version = System.getenv('RELEASE_VERSION') ?: "1.0.0"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.14.2'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.0'
    implementation group: 'net.sf.biweekly', name: 'biweekly', version: '0.6.6'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId group
            artifactId archivesBaseName
            version version

            from components.java
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST
            description 'A simple library for downloading and parsing the schedule of the selected department and group.'
            name 'bmstu-schedule-loader'
            url 'https://github.com/RomanQed/bmstu-schedule-loader'
            organization {
                name 'com.github.romanqed'
                url 'https://github.com/RomanQed/'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/RomanQed/bmstu-schedule-loader/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/RomanQed/bmstu-schedule-loader/blob/main/LICENSE'
                }
            }
            scm {
                url 'https://github.com/RomanQed/bmstu-schedule-loader'
                connection 'scm:https://github.com/RomanQed/bmstu-schedule-loader.git'
                developerConnection 'scm:https://github.com/RomanQed/bmstu-schedule-loader.git'
            }
            developers {
                developer {
                    id 'dev'
                    name 'RomanQed'
                    email 'gbakaldin@gmail.com'
                }
            }
        }
    }
}

